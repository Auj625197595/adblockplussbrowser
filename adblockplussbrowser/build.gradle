apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
        targetCompatibility = rootProject.ext.targetCompatibilityVersion
    }

    defaultConfig {
        applicationId "org.adblockplus.adblockplussbrowser"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 14
        versionName '1.1.4'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }

    flavorDimensions "region"

    productFlavors {
        world {
            dimension "region"
        }

        cn {
            dimension "region"
            applicationIdSuffix ".cn"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    afterEvaluate { project ->

        project.tasks.all { task ->
            if (task.name.matches("generate.*Release.*Resources")) {
                task.doLast {
                    updateBundledLists()
                    println('Bundled lists updated at ' + new Date().format('dd MMM yyyy, HH:mm:ss zzz'))
                }
            }
        }
    }
}

def updateBundledLists() {
    new URL(rootProject.ext.easyListUpdateUrl).withInputStream {
        i -> new File(rootProject.ext.easyListFilePath).withOutputStream { it << i }
    }
    new URL(rootProject.ext.exceptionRulesUpdateUrl).withInputStream {
        i -> new File(rootProject.ext.exceptionRulesFilePath).withOutputStream { it << i }
    }
}

dependencies {
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.11.0'
    testImplementation 'org.robolectric:robolectric:3.5.1'
    implementation "com.android.support:support-v4:$rootProject.ext.androidSupportLibraryVersion"
    implementation (group: 'commons-validator', name: 'commons-validator', version: '+') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}